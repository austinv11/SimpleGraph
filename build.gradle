plugins {
    id 'java'
    id 'idea'
    id 'maven'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.0"
}

ext {
    ver = '1.0.0-PREVIEW'
    pkg = 'com.austinv11.graphs'
}

group pkg
version ver

sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile "com.google.code.findbugs:jsr305:3.0.2"

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    failOnError false
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
}

def pomConfig = {
    licenses {
        license {
            name "MIT"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "austinv11"
            name "Austin Varela"
//            email "dev@d.com"
        }
    }

    scm {
        url "https://github.com/austinv11/SimpleGraph"
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId pkg
            artifactId 'SimpleGraph'
            version ver
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A simple Java implementation of the graph data structure.')
                root.appendNode('name', 'SimpleGraph')
                root.appendNode('url', 'https://github.com/austinv11/SimpleGraph')
                root.children().last() + pomConfig
            }
        }
    }
}
